-- Immediate Extender
--
-- Extends the immediate to the full 32 bits using sign extension (left pad with the sign bit until 32 bits).
--
-- The values of `imm_src` we're chosen arbitrarly and are generated by the `Control Unit`.

LIBRARY IEEE;
USE IEEE.STD_LOGIC_1164.ALL;
USE IEEE.NUMERIC_STD.ALL;

ENTITY immext IS
    PORT (
        instr : IN STD_LOGIC_VECTOR(31 DOWNTO 0);
        imm_src : IN STD_LOGIC_VECTOR(1 DOWNTO 0);
        imm_ext : OUT STD_LOGIC_VECTOR(31 DOWNTO 0)
    );
END ENTITY immext;

ARCHITECTURE behavioral OF immext IS
BEGIN
    imm_proc : PROCESS (instr, imm_src)
    BEGIN
        CASE imm_src IS
            WHEN "00" => -- I: sign-extend `instr[31:20]` to 32 bits
                imm_ext <= STD_LOGIC_VECTOR(RESIZE(SIGNED(instr(31 DOWNTO 20)), 32));
            WHEN "01" => -- S: sign-extend `instr[31:25] || instr[11:7]` to 32 bits
                imm_ext <= STD_LOGIC_VECTOR(RESIZE(SIGNED(instr(31 DOWNTO 25) & instr(11 DOWNTO 7)), 32));
            WHEN "10" => -- B: sign-extend `instr[31] & instr[7] & instr[30:25] & instr[11:8] & 0`
                imm_ext <= STD_LOGIC_VECTOR(RESIZE(SIGNED(instr(31) & instr(7) & instr(30 DOWNTO 25) & instr(11 DOWNTO 8) & '0'), 32));
            WHEN "11" => -- J & U:
                IF instr(6 DOWNTO 0) = "1101111" THEN -- J (jal): sign-extend `instr[31] & isntr[19:12] & instr[20] & instr[30:21] & 0` to 32 bits
                    imm_ext <= STD_LOGIC_VECTOR(RESIZE(SIGNED(instr(31) & instr(19 DOWNTO 12) & instr(20) & instr(30 DOWNTO 21) & '0'), 32));
                ELSE -- U: upper 20 bits from `instr[31:12]` || 0000 0000 0000
                    imm_ext <= STD_LOGIC_VECTOR(RESIZE(UNSIGNED(instr(31 DOWNTO 12) & "0000" & "0000" & "0000"), 32));
                END IF;
            WHEN OTHERS =>
                imm_ext <= (OTHERS => '0');
        END CASE;
    END PROCESS;
END ARCHITECTURE behavioral;

--------------------------------------------------------------------------------

LIBRARY IEEE;
USE IEEE.STD_LOGIC_1164.ALL;
USE IEEE.NUMERIC_STD.ALL;

ENTITY immext_tb IS
END ENTITY immext_tb;

ARCHITECTURE testbench OF immext_tb IS
    SIGNAL instr   : STD_LOGIC_VECTOR(31 DOWNTO 0);
    SIGNAL imm_src : STD_LOGIC_VECTOR(1 DOWNTO 0);
    SIGNAL imm_ext : STD_LOGIC_VECTOR(31 DOWNTO 0);
    SIGNAL finished: BOOLEAN := false;

    COMPONENT immext IS
        PORT (
            instr   : IN STD_LOGIC_VECTOR(31 DOWNTO 0);
            imm_src : IN STD_LOGIC_VECTOR(1 DOWNTO 0);
            imm_ext : OUT STD_LOGIC_VECTOR(31 DOWNTO 0)
        );
    END COMPONENT;

BEGIN
    uut : immext
    PORT MAP(
        instr   => instr,
        imm_src => imm_src,
        imm_ext => imm_ext
    );

    stimulus : PROCESS
    BEGIN
        instr <= (OTHERS => '0');
        imm_src <= (OTHERS => '0');
        WAIT FOR 10 ns;

        -- I (positive immediate)
        instr <= x"00500093";  -- addi x1, x0, 5 (immediate = 5)
        imm_src <= "00";       -- I-type
        WAIT FOR 10 ns;
        REPORT "I (positive immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"00000005" REPORT "I (positive immediate) failed" SEVERITY error;

        -- S (positive immediate)
        instr <= x"00502223";  -- sw x5, 4(x0) (immediate = 4)
        imm_src <= "01";       -- S-type
        WAIT FOR 10 ns;
        REPORT "S (positive immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"00000004" REPORT "S-type positive failed!" SEVERITY error;

        -- B (positive immediate)
        instr <= x"00500463";  -- beq x0, x5, 8 (immediate = 8)
        imm_src <= "10";       -- B-type
        WAIT FOR 10 ns;
        REPORT "B (positive immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"00000008" REPORT "B-type positive failed!" SEVERITY error;

        -- J (negative immediate)
        instr <= x"FF9FF0EF";  -- jal x1, -8 (immediate = -8, opcode = 1101111)
        imm_src <= "11";       -- J-type/U-type selector
        WAIT FOR 10 ns;
        REPORT "J (negative immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"FFFFFFF8" REPORT "J-type negative failed!" SEVERITY error;

        -- J (jal / positive immediate)
        instr <= x"008000EF";  -- jal x1, 8 (immediate = 8, opcode = 1101111)
        imm_src <= "11";       -- J-type/U-type selector
        WAIT FOR 10 ns;
        REPORT "(jal / positive immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"00000008" REPORT "J-type positive failed!" SEVERITY error;

        -- U (lui / positive immediate)
        instr <= x"123450B7";  -- lui x1, 0x12345 (immediate = 0x12345000, opcode = 0110111)
        imm_src <= "11";       -- J-type/U-type selector
        WAIT FOR 10 ns;
        REPORT "U (lui / positive immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"12345000" REPORT "U-type LUI failed!" SEVERITY error;

        -- U (auipc)
        instr <= x"ABCDE097";  -- auipc x1, 0xABCDE (immediate = 0xABCDE000, opcode = 0010111)
        imm_src <= "11";       -- J-type/U-type selector
        WAIT FOR 10 ns;
        REPORT "U (auipc) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"ABCDE000" REPORT "U-type AUIPC failed!" SEVERITY error;

        -- I (negative immediate)
        instr <= x"FFF00093";  -- addi x1, x0, -1 (immediate = -1)
        imm_src <= "00";       -- I-type
        WAIT FOR 10 ns;
        REPORT "I (negative immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"FFFFFFFF" REPORT "I-type negative failed!" SEVERITY error;

        -- S (negative immediate)
        instr <= x"FE502E23";  -- sw x5, -4(x0) (immediate = -4)
        imm_src <= "01";       -- S-type
        WAIT FOR 10 ns;
        REPORT "S (negative immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"FFFFFFFC" REPORT "S-type negative failed!" SEVERITY error;

        -- B (negative immediate)
        instr <= x"FE500EE3";  -- beq x0, x5, -4 (immediate = -4)
        imm_src <= "10";       -- B-type
        WAIT FOR 10 ns;
        REPORT "B (negative immediate) instr=0x" & to_hstring(instr) & " imm_ext=0x" & to_hstring(imm_ext);
        ASSERT imm_ext = x"FFFFFFFC" REPORT "B-type negative failed!" SEVERITY error;

        -- Invalid `imm_src` (should output zero)
        instr <= x"12345678";
        imm_src <= "XX";       -- Invalid
        WAIT FOR 10 ns;
        REPORT "Invalid imm_src: imm_src=XX imm_ext=0x" & to_hstring(imm_ext);

        finished <= true;
        WAIT;
    END PROCESS;

END ARCHITECTURE testbench;