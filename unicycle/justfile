_default:
    @just --list

# Create work directory
work:
    mkdir -p work

# Delete artifacts from work directory
clean:
    rm work/*

# Test all components
test-all: work
    @just test mux
    @just test adder
    @just test pc
    @just test imem

# Test individual components: mux, adder, pc, imem
test component="component": work
    @just _test-{{component}}

_test-mux:
    ghdl -a --std=08 --workdir=work src/mux.vhd
    ghdl -e --std=08 --workdir=work mux_tb
    ghdl -r --std=08 --workdir=work mux_tb --vcd=work/mux.vcd

_test-adder:
    ghdl -a --std=08 --workdir=work src/adder.vhd
    ghdl -e --std=08 --workdir=work adder_tb
    ghdl -r --std=08 --workdir=work adder_tb --vcd=work/adder.vcd

_test-pc:
    ghdl -a --std=08 --workdir=work src/pc.vhd
    ghdl -e --std=08 --workdir=work pc_register_tb
    ghdl -r --std=08 --workdir=work pc_register_tb --vcd=work/pc.vcd

_test-imem:
    ghdl -a --std=08 --workdir=work src/instruction_memory.vhd
    ghdl -e --std=08 --workdir=work instruction_memory_tb
    ghdl -r --std=08 --workdir=work instruction_memory_tb --vcd=work/imem.vcd

# Test and view waveforms for components: mux, adder, pc, imem
wave component="component": work
    @just test {{component}}
    gtkwave static/gtkwave/{{component}}.gtkw &
